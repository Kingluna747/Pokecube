
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    ivy {
        name = "thutcore"
        url = "https://github.com/Thutmose/Thut/raw/1.9/ThutCore/deobf/"
        layout "pattern", {
            artifact "[module]-${version_minecraft}-[revision]-dev.[ext]"
        }
    }
    ivy {
        name = "pokecube"
        url = "https://github.com/Thutmose/Pokecube/raw/1.9/Pokecube%20Core/deobf"
        layout "pattern", {
            artifact "[module]-${version_minecraft}-[revision]-dev.[ext]"
        }
    }
}

dependencies {
    deobfCompile name: 'thutcore', version: "${version_thutcore}", ext: 'jar'
    deobfCompile name: 'pokecube', version: "${version_pokecube}", ext: 'jar'
}

group= "pokecube"
archivesBaseName = "Pokecube Mobs"

minecraft {
    version = "${version_minecraft}-${version_forge}"
    mappings = "${version_mappings}"
    runDir = "run"
    
    useDepAts = true
    
    replace "@VERSION@", project.version
    replaceIn "PokecubeMobs.java"
    
    makeObfSourceJar = false
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'pokecube_at.cfg'
    }
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
   // archives srcJar
    archives deobfJar
}

sourceCompatibility = 1.7
targetCompatibility = 1.7